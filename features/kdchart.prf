:  # copy from environment:
  isEmpty( KDCHARTDIR ):KDCHARTDIR=$$(KDCHARTDIR)
  !isEmpty( KDCHARTDIR ) {
    unix:isEmpty(QMAKE_EXTENSION_SHLIB):QMAKE_EXTENSION_SHLIB=so
    unix:!exists( $$KDCHARTDIR/lib/libkdchart.$$QMAKE_EXTENSION_SHLIB ):error( "Cannot find libkdchart.$$QMAKE_EXTENSION_SHLIB in $KDCHARTDIR/lib" )
    #win32:!exists( $$KDCHARTDIR/lib/kdchart.lib ):error( "Cannot find kdchart.lib in $KDCHARTDIR/lib" )
    !exists( $$KDCHARTDIR/include/KDChartGlobal ):error( "Cannot find KDChartGlobal in $KDCHARTDIR/include" )

    LIBS += -L$$KDCHARTDIR/lib
    unix {
      !isEmpty(QMAKE_RPATH):LIBS += $$QMAKE_RPATH$$KDCHARTDIR/lib
      LIBS += -lkdchart
    }
    win32 {
      CONFIG(debug, debug|release) {
        LIBS += -lkdchartd
      } else {
        LIBS += -lkdchart
      }
    }

    INCLUDEPATH += $$KDCHARTDIR/include
    DEPENDPATH += $$KDCHARTDIR/include

    CONFIG += have_kdchart
    DEFINES += HAVE_KDCHART

  } else:equals( builddir, $$top_builddir ) {
    message( "WARNING: kdchart not found. Please set KDCHARTDIR either as an environment variable or on the qmake command line if you want kdchart support")
  }
